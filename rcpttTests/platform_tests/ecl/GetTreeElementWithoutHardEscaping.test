--- RCPTT testcase ---
Format-Version: 1.0
Contexts: _4zI9AWpgEeCzId1Gnkds9A,_ZQYygK2sEeCqBbDEJWufvQ,_Qck8MFqdEeSv8PFFnwFpaA
Element-Name: GetTreeElementWithoutHardEscaping
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _XvptsFjbEeSBp_l8k0N2ww
Runtime-Version: 1.5.3.qualifier
Save-Time: 10/23/14 5:46 PM
Testcase-Type: ecl

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

// setup selfaut
get-aut selfQ7 | eval { clean-self-AUT }
get-view "Test Explorer" | get-tree | select "ContextsProject/defaultWB" | get-menu Open | click
get-editor defaultWB | get-button Apply | click
get-view "Test Explorer" | get-tree | select "ContextsProject/ws" | get-menu Open | click
get-editor ws | get-button Apply | click

// rename file
get-aut selfQ7 | eval {
	
	get-view "Test Explorer" | get-tree | select "project1/text.txt" | get-menu "Rename..." | click
	with [get-window "Rename Resource"] {
    	get-editbox -after [get-label "New name:"] | set-text "text.txt([])"
    	get-button OK | click
	}
}

// switch to EscapedRegex mode
get-button "Record a Snippet" | click
with [get-window "Control Panel - selfQ7 [Recording]"] {
    get-tab-folder | get-tab-item Options | click
    get-group "Runtime Features" | get-combo -after [get-label "Table/tree item literal mode"] | select EscapedRegex
    get-tab-folder | get-tab-item Script | click
}

// click to file
get-aut selfQ7 | eval {
	get-view "Test Explorer" | get-tree | select "project1/text.txt([])"
}

// check escaping
get-window "Control Panel - selfQ7 [Recording]" | get-editbox | get-property text 
    | equals "get-view \"Test Explorer\" | get-tree | select \"project1/text.txt\\\\(\\\\[\\\\]\\\\)\"" | verify-true

// delete editor content
get-window "Control Panel - selfQ7 [Recording]" | get-button Stop | click
with [get-window "Control Panel - selfQ7"] {
    get-tab-folder | get-tab-item Options | click
    get-group "Runtime Features" | get-combo -after [get-label "Table/tree item literal mode"] | select ExactString
    get-tab-folder | get-tab-item Script | click
    get-editbox | get-menu "Select All" | click
    get-text-viewer | key-type Del
}
get-window "Control Panel - selfQ7" | get-button Record | click

// click to file
get-aut selfQ7 | eval {
	get-view "Test Explorer" | get-tree | select "project1/text.txt([])"
}

// check avoid escaping
get-window "Control Panel - selfQ7 [Recording]" | get-editbox | get-property text 
    | equals "get-view \"Test Explorer\" | get-tree | select \"project1/text.txt([])\"" | verify-true

// stop
get-window "Control Panel - selfQ7 [Recording]" | get-button Stop | click

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
